plugins {
    id 'io.micronaut.build.internal.oraclecloud-module'
}

def ociVersion = libs.oci.bom.get().version
def ociBom = new groovy.xml.XmlSlurper().parse("https://repo.maven.apache.org/maven2/com/oracle/oci/sdk/oci-java-sdk-bom/${ociVersion}/oci-java-sdk-bom-${ociVersion}.pom")

dependencies {
    annotationProcessor projects.oraclecloudSdkProcessor
    api mn.reactor
    api projects.oraclecloudSdk
    implementation mnReactor.micronaut.reactor

    // optional modules
    for (name in ociBom.dependencyManagement.dependencies.dependency.artifactId*.text()) {
        compileOnly "com.oracle.oci.sdk:$name:$ociVersion"
        testImplementation "com.oracle.oci.sdk:$name:$ociVersion"
        annotationProcessor "com.oracle.oci.sdk:$name:$ociVersion"
    }
}

compileJava.options.fork = true

tasks.withType(me.champeau.gradle.japicmp.JapicmpTask) {
    // Ignore changes in the generated factories for the new OCI Java SDK
    packageExcludes = ["io.micronaut.oraclecloud.clients.reactor.*"]
}
