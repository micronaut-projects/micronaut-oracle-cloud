buildscript {
    repositories {
        gradlePluginPortal()
        jcenter()
        maven { url "https://dl.bintray.com/micronaut/core-releases-local" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.micronaut.build:micronaut-gradle-plugins:2.0.3"
    }
}

subprojects { Project subproject ->
    group "io.micronaut.oci"

    if (subproject.name == 'oci-bom') {
        apply plugin: "java-platform"
        version projectVersion
    } else {
        apply plugin: "io.micronaut.build.common"

        dependencies {
            api platform("com.oracle.oci.sdk:oci-java-sdk-bom:$ociVersion")

            testImplementation("io.micronaut.test:micronaut-test-junit5")
            testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
            testAnnotationProcessor "io.micronaut:micronaut-inject-java"
        }

        test {
            useJUnitPlatform()
        }

         tasks.withType(JavaCompile) {
             options.encoding = "UTF-8"
             options.compilerArgs.addAll([
                     '-parameters',
                     // enables incremental compilation
                     '-Amicronaut.processing.incremental=true',
                     '-Amicronaut.processing.annotations=io.micronaut.oci.*',
                     "-Amicronaut.processing.group=$project.group",
                     "-Amicronaut.processing.module=$project.name",
             ])
         }
    }

    
    apply plugin: "io.micronaut.build.dependency-updates"

    if (subproject.parent.name == "docs-examples" || subproject.name == "docs-examples") {
        return
    }

    apply plugin: "io.micronaut.build.publishing"
}

apply plugin: "io.micronaut.build.docs"
apply plugin: "io.micronaut.build.dependency-updates"

