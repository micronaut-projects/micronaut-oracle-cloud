buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.micronaut.build.internal:micronaut-gradle-plugins:3.0.3"
        classpath "io.micronaut.gradle:micronaut-gradle-plugin:1.4.5"
    }
}

subprojects { Project subproject ->
    group "io.micronaut.oraclecloud"

    if (subproject.name == 'oraclecloud-bom') {
        apply plugin: "java-platform"
        version projectVersion
    } else if (subproject.parent.name == "docs-examples" || subproject.name == "docs-examples") {
        repositories {
            jcenter()
            mavenCentral()
        }
        return
    } else {
        apply plugin: "io.micronaut.build.internal.common"

        dependencies {
            api platform("com.oracle.oci.sdk:oci-java-sdk-bom:$ociVersion")

            testImplementation("io.micronaut.test:micronaut-test-junit5")
            testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
            testAnnotationProcessor "io.micronaut:micronaut-inject-java"
        }

         tasks.withType(JavaCompile) {
             options.encoding = "UTF-8"
             options.compilerArgs.addAll([
                     '-parameters',
                     // enables incremental compilation
                     '-Amicronaut.processing.incremental=true',
                     '-Amicronaut.processing.annotations=io.micronaut.oraclecloud.*',
                     "-Amicronaut.processing.group=$project.group",
                     "-Amicronaut.processing.module=$project.name",
             ])
         }
    }

    
    apply plugin: "io.micronaut.build.internal.dependency-updates"
    if (!['oraclecloud-sdk-processor', 'oraclecloud-atp-hikari-test', 'oraclecloud-atp-ucp-test'].contains(subproject.name)) {
        apply plugin: "io.micronaut.build.internal.publishing"
    }
}

apply plugin: "io.micronaut.build.internal.docs"
apply plugin: "io.micronaut.build.internal.dependency-updates"
