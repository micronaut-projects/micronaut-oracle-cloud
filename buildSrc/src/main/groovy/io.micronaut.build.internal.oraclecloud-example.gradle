plugins {
    id 'io.micronaut.minimal.application'
    id 'io.micronaut.build.internal.oraclecloud-base'
}

micronaut {
    version libs.versions.micronaut.platform.get()
    coreVersion.set(libs.versions.micronaut.asProvider().get())
}
dependencies {
    implementation(libs.oci.objectstorage) {
        exclude group: 'org.javassist', module: 'javassist'
    }
    annotationProcessor mnValidation.micronaut.validation.processor
    implementation mnValidation.micronaut.validation
    implementation projects.micronautOraclecloudSdk
    testImplementation mn.micronaut.http.client
    testRuntimeOnly mn.micronaut.jackson.databind
}

if (JavaVersion.current().isCompatibleWith(JavaVersion.toVersion(19))) {
    plugins.withId('org.graalvm.buildtools.native') {
        graalvmNative.binaries.all {
            buildArgs.add('--enable-preview')
        }
    }
}

tasks.named("check") { task ->
    def graal = ["jvmci.Compiler", "java.vendor.version", "java.vendor"].any {
        System.getProperty(it)?.toLowerCase(Locale.ENGLISH)?.contains("graal")
    }
    if (graal) {
        task.dependsOn("nativeTest")
    }
}

