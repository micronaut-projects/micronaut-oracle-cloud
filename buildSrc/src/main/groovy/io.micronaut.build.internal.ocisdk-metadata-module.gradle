import io.micronaut.oraclecloud.gradle.GenerateClientFactories

plugins {
    id "io.micronaut.build.internal.module"
    id 'io.micronaut.build.internal.ocisdk-metadata-published'
}

dependencies {
    annotationProcessor(projects.micronautOraclecloudSerdeProcessor)
    // The dependency is required for annotations and their metadata
    implementation(mnSerde.micronaut.serde.api)
}

repositories {
    mavenCentral()
}

sourceSets {
    generated {
        java {}
    }
}

if (!project.name.contains("circuitbreaker")) {
    var generateClientFactories = tasks.register("generateClientFactories", GenerateClientFactories) {
        sources = sourceSets.main.java
        packageName.convention("io.micronaut.oraclecloud.clients")
        baseOutputDirectory.convention(layout.buildDirectory.dir("generatedClientFactories"))
    }

    sourceSets.generated.java.srcDir(generateClientFactories)

    dependencies {
        generatedAnnotationProcessor(projects.micronautOraclecloudSdkProcessor)
        generatedCompileOnly(sourceSets.main.output)
        generatedCompileOnly(projects.micronautOraclecloudCommon)
        generatedCompileOnly(projects.micronautOraclecloudSdkBase)
        generatedCompileOnly(mnRxjava2.rxjava2)
        generatedCompileOnly(mn.reactor)
        generatedCompileOnly(mnReactor.micronaut.reactor)
    }
}

def metadataJar = tasks.register("metadataJar", Jar) {
    archiveClassifier = "metadata"
    from(sourceSets.main.output) {
        include("META-INF/micronaut/**")
        include { e ->
            if (e.file.directory) {
                return true
            }
            def fn = e.file.name
            return fn.contains('$Introspection')
        }
    }
    from(sourceSets.generated.output) {
        exclude("**/SdkProcessorDummy**")
    }
}

configurations.all {
    if (name == "metadataElements") {
        outgoing.artifact(metadataJar) {
            classifier = null
        }
    }
}

tasks.withType(Test).configureEach {
    // We don't convert tests from the OCI SDK
    enabled = false
}

tasks.withType(Checkstyle).configureEach {
    // The checkstyle rules of this project differ
    enabled = false
}

tasks.named("prepareJavadocAggregation") {
    // not relevant for us
    enabled = false
}
tasks.withType(Javadoc).configureEach {
    // not relevant for us
    enabled = false
}

tasks.named("sourcesJar", Jar) {
    // our modules don't have sources, but the sources jar is required for Maven Central
    // so we build an empty one
    exclude "**/*"
}

tasks.named("compileTestJava") {
    enabled = false
}

tasks.configureEach {
    if (name.startsWith("spotless")) {
        // The license and checkstyle rules differ from ours
        enabled = false
    }
}

micronautBuild {
    binaryCompatibility.enabled = false
}

configurations.all {
    resolutionStrategy.preferProjectModules()
    ResolutionStrategy rs = resolutionStrategy
    rs.dependencySubstitution.all {
        if (requested instanceof ModuleComponentSelector) {
            if (requested.group == "com.oracle.oci.sdk") {
                def simpleName = requested.module - 'oci-java-sdk-'
                if (simpleName == 'bom') {
                    rs.dependencySubstitution {
                        substitute(platform(requested)).using(platform(project(":micronaut-oraclecloud-bom")))
                    }
                } else if (!requested.module.contains("-common")) {
                    useTarget(project(":micronaut-oraclecloud-bmc-${simpleName}"))
                }
            }
        }
    }
}
