plugins {
    id 'io.micronaut.build.internal.oraclecloud-module'
    id 'io.micronaut.build.internal.oraclecloud-bom'
}

dependencies {
    annotationProcessor projects.micronautOraclecloudSdkProcessor
    api projects.micronautOraclecloudSdk
    compileOnly projects.micronautOraclecloudSdkBase
    implementation mnRxjava2.rxjava2

    // optional modules
    for (name in gradle.ociArtifacts) {
        compileOnly "com.oracle.oci.sdk:$name:$gradle.ociVersion"
        testImplementation "com.oracle.oci.sdk:$name:$gradle.ociVersion"
        annotationProcessor "com.oracle.oci.sdk:$name:$gradle.ociVersion"
    }
}

compileJava.options.fork = true
compileJava.options.compilerArgs.add("-AociSdkClientClasses=$gradle.ociSdkClientClasses")

tasks.withType(me.champeau.gradle.japicmp.JapicmpTask) {
    // Ignore changes in the generated factories for the new OCI Java SDK
    packageExcludes = ["io.micronaut.oraclecloud.clients.rxjava2.*"]
}
